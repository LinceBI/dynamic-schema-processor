plugins {
	id('java-library')
	id('maven-publish')
}

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

defaultTasks('clean', 'build')

if (versionRef != '') {
	version = "$version-$versionRef"
}

if (versionRelease != 'true') {
	version = "$version-SNAPSHOT"
}

dependencies {
	implementation("pentaho:pentaho-platform-api:$biserverVersion") { transitive = false }
	implementation("pentaho:pentaho-platform-core:$biserverVersion") { transitive = false }
	implementation("pentaho:mondrian:$biserverVersion") { transitive = false }
}

repositories {
	mavenLocal()
	mavenCentral()
	maven {
		url('https://repo.stratebi.com/repository/pentaho-mvn/')
	}
}

publishing {
	publications {
		maven(MavenPublication) {
			groupId = "$group"
			artifactId = "$artifact"
			from(components.java)
		}
	}
	repositories {
		maven {
			def repoBase = 'https://repo.stratebi.com/repository'
			def repoName = version.endsWith('SNAPSHOT')
				? 'lincebi-mvn-snapshots'
				: 'lincebi-mvn-releases'
			url = "$repoBase/$repoName"
			credentials {
				username(System.getenv('REPO_MAVEN_LINCEBI_RW_USER'))
				password(System.getenv('REPO_MAVEN_LINCEBI_RW_PASSWORD'))
			}
		}
	}
}
